import numpy as np


orders = (np.ceil(np.exp(np.linspace(np.log(20), np.log(500), 8)) / 10) * 10).astype(int)
Ns = np.logspace(2, 5, 8).astype(int)

radius = 0.1
u = (1.0,1.0)

rule all:
    input:
        "figures/speed_vs_order.pdf",
        "figures/speed_vs_N.pdf"

rule flux_starry_quadratic:
    output:
        "data/flux_starry_quadratic_{N}.npz"
    params:
        radius = radius,
        u = u
    script:
        "scripts/flux_starry.py"

rule flux_starry_l20:
    output:
        "data/flux_starry_l20_{N}.npz"
    params:
        radius = radius,
        u = None
    script:
        "scripts/flux_starry.py"


rule flux_exoplanet_quadratic:
    output:
        "data/flux_exoplanet_quadratic_{N}.npz"
    params:
        radius = radius,
        u = u
    script:
        "scripts/flux_exoplanet_quadratic.py"


rule flux_jaxoplanet_quadratic:
    output:
        "data/flux_jaxoplanet_quadratic_{N}_{order}.npz"
    params:
        radius = radius,
        u = u
    script:
        "scripts/flux_jaxoplanet_quadratic.py"


rule flux_jaxoplanet_l20:
    output:
        "data/flux_jaxoplanet_l20_{N}_{order}.npz"
    params:
        radius = radius,
        u = None
    script:
        "scripts/flux_jaxoplanet.py"


rule plot_error_vs_order:
    input:
        starry_quadratic = "data/flux_starry_quadratic_10000.npz",
        jaxoplanet_quadratic = [f"data/flux_jaxoplanet_quadratic_10000_{order}.npz" for order in orders],
        jaxoplanet_quadratic_gpu = [f"static/gpu/flux_jaxoplanet_quadratic_10000_{order}.npz" for order in orders],
        exoplanet_quadratic = "data/flux_exoplanet_quadratic_10000.npz",
        starry_l20 = "data/flux_starry_l20_10000.npz",
        jaxoplanet_l20 = [f"data/flux_jaxoplanet_l20_10000_{order}.npz" for order in orders],
        jaxoplanet_l20_gpu = [f"static/gpu/flux_jaxoplanet_l20_10000_{order}.npz" for order in orders]
    output:
        "figures/speed_vs_order.pdf"
    params:
        orders=orders
    script:
        "scripts/plot_error_vs_order.py"

fixed_order_quad = 100
fix_order_l20 = 200

rule flux_all_single_b:
    params:
        orders=orders,
        radius = radius,
        u = u
    output:
        "data/flux_all_single_b.pickle"
    script:
        "scripts/flux_all_single_b.py"

rule plot_error_vs_N:
    input:
        starry_quadratic = [f"data/flux_starry_quadratic_{N}.npz" for N in Ns],
        jaxoplanet_quadratic = [f"data/flux_jaxoplanet_quadratic_{N}_{fixed_order_quad}.npz" for N in Ns],
        jaxoplanet_quadratic_gpu = [f"static/gpu/flux_jaxoplanet_quadratic_{N}_{fixed_order_quad}.npz" for N in Ns],
        exoplanet_quadratic = [f"data/flux_exoplanet_quadratic_{N}.npz" for N in Ns],
        starry_l20 = [f"data/flux_starry_l20_{N}.npz" for N in Ns],
        jaxoplanet_l20 = [f"data/flux_jaxoplanet_l20_{N}_{fix_order_l20}.npz" for N in Ns],
        jaxoplanet_l20_gpu = [f"static/gpu/flux_jaxoplanet_l20_{N}_{fix_order_l20}.npz" for N in Ns]
    output:
        "figures/speed_vs_N.pdf"
    params:
        Ns=Ns,
        order_l20=fix_order_l20,
        order_quad=fixed_order_quad
    script:
        "scripts/plot_speed_vs_N.py"
